include_guard(GLOBAL)

include(FetchContent)

function(_pivot_common_boost_ensure_headers_target boost_root)
    if(TARGET Boost::headers)
        return()
    endif()
    if(TARGET Boost::boost)
        add_library(Boost::headers ALIAS Boost::boost)
        return()
    endif()
    add_library(boost_headers INTERFACE)
    target_include_directories(boost_headers INTERFACE "${boost_root}")
    add_library(Boost::headers ALIAS boost_headers)
endfunction()

function(pivot_common_setup_boost)
    set(options PREFER_STATIC REQUIRE_STATIC)
    set(oneValueArgs MIN_VERSION BOOST_VERSION)
    set(multiValueArgs COMPONENTS)
    cmake_parse_arguments(PIVOT_BOOST "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    if(NOT PIVOT_BOOST_MIN_VERSION)
        set(PIVOT_BOOST_MIN_VERSION 1.75)
    endif()
    if(NOT PIVOT_BOOST_COMPONENTS)
        set(PIVOT_BOOST_COMPONENTS filesystem system)
    endif()

    if(PIVOT_BOOST_PREFER_STATIC)
        set(Boost_USE_STATIC_LIBS ON)
    endif()
    set(Boost_USE_MULTITHREADED ON)

    find_package(Boost ${PIVOT_BOOST_MIN_VERSION} QUIET COMPONENTS ${PIVOT_BOOST_COMPONENTS})

    if(Boost_FOUND)
        if(NOT TARGET Boost::headers)
            if(TARGET Boost::boost)
                add_library(Boost::headers ALIAS Boost::boost)
            else()
                add_library(boost_headers INTERFACE)
                if(DEFINED Boost_INCLUDE_DIRS)
                    target_include_directories(boost_headers INTERFACE ${Boost_INCLUDE_DIRS})
                endif()
                add_library(Boost::headers ALIAS boost_headers)
            endif()
        endif()
        if(PIVOT_BOOST_REQUIRE_STATIC AND NOT WIN32)
            foreach(comp IN LISTS PIVOT_BOOST_COMPONENTS)
                if(TARGET Boost::${comp})
                    get_target_property(_loc Boost::${comp} IMPORTED_LOCATION_RELEASE)
                    if(NOT _loc)
                        get_target_property(_loc Boost::${comp} IMPORTED_LOCATION)
                    endif()
                    if(_loc AND NOT _loc MATCHES "\\.a$")
                        message(FATAL_ERROR "Boost::${comp} is not a static library: ${_loc}")
                    endif()
                endif()
            endforeach()
        endif()
        return()
    endif()

    if(NOT PIVOT_BOOST_BOOST_VERSION)
        set(PIVOT_BOOST_BOOST_VERSION 1.83.0)
    endif()
    string(REPLACE "." "_" BOOST_VERSION_U "${PIVOT_BOOST_BOOST_VERSION}")

    FetchContent_Declare(
        pivot_boost_ext
        URL https://archives.boost.io/release/${PIVOT_BOOST_BOOST_VERSION}/source/boost_${BOOST_VERSION_U}.tar.gz
        DOWNLOAD_EXTRACT_TIMESTAMP OFF
    )
    FetchContent_MakeAvailable(pivot_boost_ext)

    _pivot_common_boost_ensure_headers_target("${pivot_boost_ext_SOURCE_DIR}")

    foreach(comp IN LISTS PIVOT_BOOST_COMPONENTS)
        if(comp STREQUAL "system")
            if(NOT TARGET Boost::system)
                file(GLOB BOOST_SYSTEM_SRCS "${pivot_boost_ext_SOURCE_DIR}/libs/system/src/*.cpp")
                if(NOT BOOST_SYSTEM_SRCS)
                    message(FATAL_ERROR "Failed to locate Boost.System sources under ${pivot_boost_ext_SOURCE_DIR}")
                endif()
                add_library(boost_system_static STATIC ${BOOST_SYSTEM_SRCS})
                target_include_directories(boost_system_static PUBLIC "${pivot_boost_ext_SOURCE_DIR}")
                target_compile_features(boost_system_static PUBLIC cxx_std_20)
                set_target_properties(boost_system_static PROPERTIES POSITION_INDEPENDENT_CODE ON)
                if(MSVC)
                    target_compile_definitions(boost_system_static PRIVATE BOOST_ALL_NO_LIB BOOST_INTERPROCESS_NO_LIB)
                endif()
                add_library(Boost::system ALIAS boost_system_static)
            endif()
        elseif(comp STREQUAL "filesystem")
            if(NOT TARGET Boost::filesystem)
                file(GLOB BOOST_FILESYSTEM_SRCS "${pivot_boost_ext_SOURCE_DIR}/libs/filesystem/src/*.cpp")
                if(NOT BOOST_FILESYSTEM_SRCS)
                    message(FATAL_ERROR "Failed to locate Boost.Filesystem sources under ${pivot_boost_ext_SOURCE_DIR}")
                endif()
                add_library(boost_filesystem_static STATIC ${BOOST_FILESYSTEM_SRCS})
                target_include_directories(boost_filesystem_static PUBLIC "${pivot_boost_ext_SOURCE_DIR}")
                target_compile_features(boost_filesystem_static PUBLIC cxx_std_20)
                set_target_properties(boost_filesystem_static PROPERTIES POSITION_INDEPENDENT_CODE ON)
                target_link_libraries(boost_filesystem_static PUBLIC Boost::system)
                if(MSVC)
                    target_compile_definitions(boost_filesystem_static PRIVATE BOOST_ALL_NO_LIB BOOST_INTERPROCESS_NO_LIB)
                endif()
                add_library(Boost::filesystem ALIAS boost_filesystem_static)
            endif()
        elseif(comp STREQUAL "json")
            if(NOT TARGET Boost::json)
                file(GLOB BOOST_JSON_SRCS "${pivot_boost_ext_SOURCE_DIR}/libs/json/src/*.cpp")
                if(NOT BOOST_JSON_SRCS)
                    message(FATAL_ERROR "Failed to locate Boost.JSON sources under ${pivot_boost_ext_SOURCE_DIR}")
                endif()
                add_library(boost_json_objects OBJECT ${BOOST_JSON_SRCS})
                target_include_directories(boost_json_objects PRIVATE "${pivot_boost_ext_SOURCE_DIR}")
                target_compile_features(boost_json_objects PRIVATE cxx_std_17)
                set_target_properties(boost_json_objects PROPERTIES POSITION_INDEPENDENT_CODE ON)
                if(MSVC)
                    target_compile_definitions(boost_json_objects PRIVATE BOOST_ALL_NO_LIB BOOST_INTERPROCESS_NO_LIB)
                endif()
                add_library(Boost::json ALIAS boost_json_objects)
            endif()
        elseif(comp STREQUAL "interprocess")
            # Header-only; satisfied by Boost::headers.
        else()
            message(FATAL_ERROR "pivot_common_setup_boost fallback only supports: system, filesystem, json, interprocess. Requested: ${comp}")
        endif()
    endforeach()
endfunction()
